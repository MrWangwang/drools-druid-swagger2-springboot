package com.xu.drools;
import com.xu.drools.bean.PointDomain
dialect  "mvel"
//过生日，加10分，并且当月交易笔数翻倍，再计算积分
// 2011-01-08 - 2011-08-08每月信用卡还款3次以上，每满3笔赠送30分
// 当月购物总金额100以上，每100元赠送10分
// 当月购物次数5次以上，每五次赠送50分
// 特别的，如果全部满足了要求，则额外奖励100分
rule birthdayPoint
    //过生日，加10分，并且当月交易笔数翻倍，再计算积分
    salience 100
    lock-on-active true
    when
        $pointDomain : PointDomain(birthDay == true)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+10);
        $pointDomain.setBuyNums($pointDomain.getBuyNums()*2);
        $pointDomain.setBillThisMonth($pointDomain.getBillThisMonth()*2);
        $pointDomain.recodePointLog($pointDomain.getUserName(),"生日积分");
end

rule billThisMonthPoint
    // 2011-01-08 - 2011-08-08每月信用卡还款3次以上，每满3笔赠送30分
    salience 99
    lock-on-active true
    date-effective "2011-01-08 23:59:59"
    date-expires "2011-08-08 23:59:59"
    when
        $pointDomain : PointDomain(billThisMonth >= 3)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+$pointDomain.getBillThisMonth()/3*30);
        $pointDomain.recodePointLog($pointDomain.getUserName(),"信用卡还款积分");
end

rule buyMoneyPoint
   // 当月购物总金额1000以上，每100元赠送10分
   salience 98
   lock-on-active true
   when
        $pointDomain : PointDomain(buyMoney>=1000)
   then
        $pointDomain.setPoint($pointDomain.getPoint()+(int)($pointDomain.getBuyMoney()/100)*10);
        $pointDomain.recodePointLog($pointDomain.getUserName(),"购物总金额满1000积分");
end

rule buyNumPoint
    //当月购物次数5次以上，每五次赠送50分
    when
        $pointDomain : PointDomain(buyNums >= 5)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+$pointDomain.getBackNums()/5*50);
        $pointDomain.recodePointLog($pointDomain.getUserName(),"当月购物满5次积分");
end

rule allfitPoint
    // 特别的，如果全部满足了要求，则额外奖励100分
    when
        $pointDomain : PointDomain(buyMoney>=1000 && buyNums >=5 && billThisMonth >=3)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+100);
        $pointDomain.recodePointLog($pointDomain.getUserName(),"全部满足条件积分");
end